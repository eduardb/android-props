ext {
    versions = [
            androidSdk    : [
                    compile   : 27,
                    target    : 27,
                    min       : 21,
                    buildTools: '27.0.3'
            ],
            anko          : '0.10.5',
            archComponents: '1.1.1',
            dagger        : '2.15',
            koin          : '0.9.2',
            kotlin        : '1.2.41',
            okHttp        : '3.9.1',
            retrofit      : '2.3.0',
            rxBinding     : '2.1.1',
            supportLibrary: '27.1.1',

            mockito       : '2.18.3',
            espresso      : '3.0.2'
    ]

    gradlePlugins = [
            androidTools  : 'com.android.tools.build:gradle:3.1.2',
            easylauncher  : 'com.akaita.android:easylauncher:1.2.0',
            googleServices: 'com.google.gms:google-services:3.2.1', // 3.3.0
            kotlin        : "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
            shot          : 'com.karumi:shot:2.0.0'
    ]

    libraries = [
            app    : [
                    androidKtx                  : 'androidx.core:core-ktx:0.3',
                    ankoCommons                 : "org.jetbrains.anko:anko-commons:${versions.anko}",
                    archLifecycleExtensions     : "android.arch.lifecycle:extensions:${versions.archComponents}",
                    archLifecycleCompiler       : "android.arch.lifecycle:compiler:${versions.archComponents}",
                    archLifecycleReactiveStreams: "android.arch.lifecycle:reactivestreams:${versions.archComponents}",

                    dagger                      : "com.google.dagger:dagger:${versions.dagger}",
                    daggerAndroid               : "com.google.dagger:dagger-android:${versions.dagger}",
                    daggerAndroidProcessor      : "com.google.dagger:dagger-android-processor:${versions.dagger}",
                    daggerAndroidSupport        : "com.google.dagger:dagger-android-support:${versions.dagger}",
                    daggerCompiler              : "com.google.dagger:dagger-compiler:${versions.dagger}",

                    firebaseCore                : 'com.google.firebase:firebase-core:15.0.2',

                    javaxAnnotations            : 'org.glassfish:javax.annotation:10.0-b28',

                    koin                        : "org.koin:koin-core:${versions.koin}",
                    koinAndroid                 : "org.koin:koin-android:${versions.koin}",
                    koinAndroidArchitecture     : "org.koin:koin-android-architecture:${versions.koin}",

                    okHttpClient                : "com.squareup.okhttp3:okhttp:${versions.okHttp}",
                    okHttpLoggingInterceptor    : "com.squareup.okhttp3:logging-interceptor:${versions.okHttp}",

                    picasso                     : 'com.squareup.picasso:picasso:2.5.2',
                    picasso2OkHttp3Downloader   : 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0',

                    retrofit                    : "com.squareup.retrofit2:retrofit:${versions.retrofit}",
                    retrofitGsonConverter       : "com.squareup.retrofit2:converter-gson:${versions.retrofit}",
                    retrofitRxJava2Adapter      : "com.squareup.retrofit2:adapter-rxjava2:${versions.retrofit}",
                    rxAndroid                   : 'io.reactivex.rxjava2:rxandroid:2.0.2',
                    rxBindingKotlin             : "com.jakewharton.rxbinding2:rxbinding-kotlin:${versions.rxBinding}",
                    rxBindingDesignKotlin       : "com.jakewharton.rxbinding2:rxbinding-kotlin:${versions.rxBinding}",
                    rxJava2                     : 'io.reactivex.rxjava2:rxjava:2.1.13',
                    rxKotlin                    : 'io.reactivex.rxjava2:rxkotlin:2.1.0',
                    rxRelay                     : 'com.jakewharton.rxrelay2:rxrelay:2.0.0'
            ],

            kotlin : [
                    reflect   : "org.jetbrains.kotlin:kotlin-reflect:${versions.kotlin}",
                    stdLib    : "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
                    stdLibJdk7: "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"
            ],

            support: [
                    annotations            : "com.android.support:support-annotations:${versions.supportLibrary}",
                    appCompat              : "com.android.support:appcompat-v7:${versions.supportLibrary}",
                    cardView               : "com.android.support:cardview-v7:${versions.supportLibrary}",
                    constraintLayout       : 'com.android.support.constraint:constraint-layout:1.0.2',
                    design                 : "com.android.support:design:${versions.supportLibrary}",
                    fragment               : "com.android.support:support-fragment:${versions.supportLibrary}",
                    multidex               : 'com.android.support:multidex:1.0.2',
                    multidexInstrumentation: 'com.android.support:multidex-instrumentation:1.0.2',
                    recyclerview           : "com.android.support:recyclerview-v7:${versions.supportLibrary}",
                    v4                     : "com.android.support:support-v4:${versions.supportLibrary}",
                    v7                     : "com.android.support:support-v7:${versions.supportLibrary}",
            ],

            test   : [
                    junit          : 'junit:junit:4.12',
                    kluent         : 'org.amshove.kluent:kluent:1.38',
                    koinTest       : "org.koin:koin-test:${versions.koin}",
                    kotlinTestJunit: "org.jetbrains.kotlin:kotlin-test-junit:${versions.kotlin}",
                    mockitoCore    : "org.mockito:mockito-core:${versions.mockito}",
                    mockitoKotlin  : 'com.nhaarman:mockito-kotlin:1.5.0'
            ],

            uiTest : [
                    archCoreTesting     : "android.arch.core:core-testing:${versions.archComponents}",
                    dexOpener           : 'com.github.tmurakami:dexopener:0.12.1',
                    espressoContrib     : "com.android.support.test.espresso:espresso-contrib:${versions.espresso}",
                    espressoCore        : "com.android.support.test.espresso:espresso-core:${versions.espresso}",
                    espressoIntents     : "com.android.support.test.espresso:espresso-intents:${versions.espresso}",
                    fbScreenshotTesting : 'com.facebook.testing.screenshot:core:0.6.0',
                    mockWebServer       : "com.squareup.okhttp3:mockwebserver:${versions.okHttp}",
                    mockitoAndroid      : "org.mockito:mockito-android:${versions.mockito}",
                    okHttpIdlingResource: 'com.jakewharton.espresso:okhttp3-idling-resource:1.0.0',
                    supportTestRunner   : 'com.android.support.test:runner:1.0.2'
            ]
    ]

    commonTestDependencies = {
        it.testImplementation libraries.test.junit
        it.testImplementation libraries.test.kluent
        it.testImplementation libraries.test.kotlinTestJunit
        it.testImplementation libraries.test.mockitoKotlin
    }

    commonTestDependenciesJavaOnly = {
        it.testCompile libraries.test.junit
        it.testCompile libraries.test.kluent
        it.testCompile libraries.test.kotlinTestJunit
        it.testCompile libraries.test.mockitoKotlin
    }

    commonAndroidTestDependencies = {
        it.androidTestImplementation libraries.test.mockitoKotlin
        it.androidTestImplementation libraries.uiTest.archCoreTesting
        it.androidTestImplementation libraries.uiTest.mockitoAndroid
        it.androidTestImplementation libraries.uiTest.dexOpener
        it.androidTestImplementation(libraries.uiTest.espressoContrib, {
            exclude group: 'com.android.support'
            exclude group: 'com.google.code.findbugs'
        })
        it.androidTestImplementation(libraries.uiTest.espressoCore, {
            exclude group: 'com.android.support', module: 'support-annotations'
            exclude group: 'com.google.code.findbugs'
        })
        it.androidTestImplementation(libraries.uiTest.espressoIntents, {
            exclude group: 'com.android.support', module: 'support-annotations'
            exclude group: 'com.google.code.findbugs'
        })
        it.androidTestImplementation(libraries.uiTest.supportTestRunner, {
            exclude group: 'com.android.support', module: 'support-annotations'
        })
        it.androidTestImplementation(libraries.uiTest.fbScreenshotTesting) {
            exclude group: 'com.crittercism.dexmaker', module: 'dexmaker'
            exclude group: 'com.crittercism.dexmaker', module: 'dexmaker-dx'
        }
    }
}

subprojects {
    configurations.all {
        resolutionStrategy {
            forcedModules = [
                    "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin",
                    "org.jetbrains.kotlin:kotlin-reflect:$versions.kotlin"
            ]
        }
    }
}
